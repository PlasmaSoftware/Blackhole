plugins {
    id 'java'
    id 'idea'
    id 'maven-publish'
    id "com.jfrog.bintray" version "1.8.0"
}

apply plugin: 'java-library'
apply from: 'jdks.gradle'

ext {
    ver = '1.0.0'
    pkg = 'plasma.blackhole'
}

group pkg
version ver

repositories {
    jcenter()
}

sourceSets {
    java9 {
        java {
            srcDirs = ['src/main/java9']
        }
    }
}

dependencies {
    java9Implementation files(sourceSets.main.output.classesDirs) { builtBy compileJava }

    compile 'com.squareup:javapoet:1.11.1'
    compile 'net.openhft:compiler:2.3.1'
//    compile 'de.ruedigermoeller:fst:2.57'
    compile 'com.google.code.findbugs:jsr305:3.0.2'
//    compile 'com.austinv11.graphs:SimpleGraph:1.0.0'
//    compile 'org.objenesis:objenesis:3.0.1'
//    compile 'org.apache.commons:commons-collections4:4.2'
    compile "javax.annotation:javax.annotation-api:1.2"
    compile 'com.austinv11.servicer:Servicer:1.0.0'

    annotationProcessor 'com.austinv11.servicer:Servicer:1.0.0'

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile 'com.google.testing.compile:compile-testing:0.15'
    testCompile 'com.google.truth:truth:0.42'
    testCompile "javax.annotation:javax.annotation-api:1.2"
    testCompile 'com.austinv11.servicer:Servicer:1.0.0'

    testAnnotationProcessor 'com.austinv11.servicer:Servicer:1.0.0'
}

compileJava {
    sourceCompatibility = 8
    targetCompatibility = 8
}

compileJava9Java {
    sourceCompatibility = 9
    targetCompatibility = 9
}

jar {
    into('META-INF/versions/9') {
        from sourceSets.java9.output
    }
    manifest.attributes(
            'Multi-Release': 'true'
    )
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

javadoc {
    failOnError false
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            pom {
                name = 'Blackhole'
                description = 'Exploiting annotation processing for DI, validation and more!'
                url = 'https://github.com/PlasmaSoftware/Blackhole'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'austinv11'
                        name = 'Austin Varela'
                        email = 'austinv11@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/PlasmaSoftware/Blackhole.git'
                    url = 'https://github.com/PlasmaSoftware/Blackhole'
                }
            }
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId pkg
            artifactId 'Blackhole'
            version ver
        }
    }
}